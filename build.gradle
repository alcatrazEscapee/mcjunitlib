buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
    }
}

plugins {
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'maven-publish'
	id 'idea'
    id 'java'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'

// Allow local mappings customization
def localProperties = new File('./dev.gradle')
localProperties.createNewFile()

apply from: 'dev.gradle'

def mappingsChannel = project.hasProperty('mappings_channel') ? mappings_channel : 'official'
def mappingsVersion = project.hasProperty('mappings_version') ? mappings_version : minecraft_version
println 'Using mappings: ' + mappingsChannel + ' / ' + mappingsVersion

version = '1.5.7-1.17.1'
println 'Version: ' + version

group = 'com.alcatrazescapee.mcjunitlib'
archivesBaseName = "mcjunitlib-${minecraft_version}"
java.toolchain.languageVersion = JavaLanguageVersion.of(16)

minecraft {
    mappings channel: mappingsChannel, version: mappingsVersion
}

repositories {
	mavenLocal()
    mavenCentral()
}

configurations {
    // Implementation dependencies that should also be packaged in the fat jar
    implementation.extendsFrom(implementationPackage)
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    implementationPackage 'org.junit.platform:junit-platform-launcher:1.6.2'
    implementationPackage 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    implementationPackage 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

jar.finalizedBy('reobfJar')

jar {
    manifest {
        attributes([
            "Implementation-Version": "${project.version}",
            "FMLModType": "LIBRARY"
        ])
    }

    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    // Include the full JUnit classpath in the jar
    from {
        configurations.implementationPackage.filter({ File file ->
            file.exists()
        }).collect({ File file ->
            file.isDirectory() ? file : zipTree(file)
        })
    }
}

// Automatically apply a license header when running checkLicense / updateLicense
license {
    header = project.file('HEADER.txt')

    include '**/*.java'
}

java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = 'com.github.alcatrazEscapee'
            artifactId = "mcjunitlib"
            version = "${version}"

            // Filter any dependencies from appearing in the pom.xml
            pom.withXml {
                asNode().dependencies.dependency.each { dep ->
                    assert dep.parent().remove(dep)
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}